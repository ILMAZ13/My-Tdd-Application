apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'gnag'
apply plugin: 'io.fabric'
apply plugin: "androidx.navigation.safeargs"

def securePropertiesFile = rootProject.file("secure.properties")
def secureProperties = new Properties()
secureProperties.load(new FileInputStream(securePropertiesFile))

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.flatstack.mytddapplication"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 2
        versionName "1.0.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        release {
            keyAlias secureProperties['keyAlias']
            keyPassword secureProperties['keyPassword']
            storeFile file(secureProperties['storeFile'])
            storePassword secureProperties['storePassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    buildTypes.each {
        it.buildConfigField 'String', 'BASE_URL', baseUrl
        it.buildConfigField 'String', 'OMDB_API_KEY', secureProperties['omdbApiKey']
        it.manifestPlaceholders = [fabric_key: secureProperties['fabric_key']]
    }
}

ext {
    kodein_version = "6.0.1"
    retrofit_version = "2.5.0"
    okhttp_version = "3.12.0"
    room_version = "2.1.0-alpha03"
    paging_version = "2.1.0-rc01"
    glide_version = "4.8.0"
    lifecycle_version = "2.0.0"
    nav_version = "1.0.0-alpha08"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    //Kodein
    implementation "org.kodein.di:kodein-di-generic-jvm:$kodein_version"
    implementation "org.kodein.di:kodein-di-framework-android-x:$kodein_version"
    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    //OkHttp
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    //Room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    //Paging
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    //Glide
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"
    //Lifecycle
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"
    //Navigation
    implementation "android.arch.navigation:navigation-fragment-ktx:$nav_version"
    implementation "android.arch.navigation:navigation-ui-ktx:$nav_version"
    //Fabric
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.7@aar') {
        transitive = true
    }

    implementation 'com.google.android.material:material:1.1.0-alpha02'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0-alpha01'
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}

gnag {
    enabled true
    failOnError true

    checkstyle {
        enabled true
        reporterConfig project.file('../quality/checkstyle-config.xml')
    }

    pmd {
        enabled true
        reporterConfig project.file('../quality/pmd-ruleset.xml')
    }

    findbugs {
        enabled false
    }

    ktlint {
        enabled true
        toolVersion "0.29.0"
    }

    detekt {
        enabled false
    }

    androidLint {
        enabled true
        severity 'Error'
    }

    github {
        repoName 'ILMAZ13/My-Tdd-Application'
    }
}

tasks.withType(Test) {
    testLogging {
        events "started", "passed", "skipped", "failed"
    }
}
